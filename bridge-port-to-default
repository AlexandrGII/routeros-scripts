#!rsc
# RouterOS script: bridge-port-to-default
# Copyright (c) 2013-2018 Christian Hesse <mail@eworm.de>
#
# reset bridge ports to default bridge

:global "bridge-port-to";

:local "len" ([ :len $"bridge-port-to" ] + 1);

:foreach interface in=[ / interface bridge port find where comment!="" ] do={
  :foreach comment in=[ :toarray [ / interface bridge port get $interface comment ] ] do={
    :if ([ :pick $comment 0 ] = " ") do={
      :set comment [ :pick $comment 1 [ :len $comment ] ];
    }
    :if ([ :pick $comment 0 $len ] = ($"bridge-port-to" . ":")) do={
      :local "interface-name" [ / interface bridge port get $interface interface ];
      :local "bridge-default" [ :pick $comment $len [ :len $comment ] ];
      :local "bridge-current" [ / interface bridge port get $interface bridge ];
      :if ($"bridge-default" != $"bridge-current") do={
        :log info ("Changing interface " . $"interface-name" . " to " . $"bridge-port-to" . " bridge " . $"bridge-default");
        / interface bridge port set bridge=$"bridge-default" $interface;
        / ip dhcp-client renew [ find where interface=$"bridge-default" ];
      } else={
        :log debug ("Interface " . $"interface-name" . " already connected to " . $"bridge-port-to" . " bridge " . $"bridge-default");
      }
    }
  }
}
