#!rsc
# RouterOS script: bridge-port-to-default
# Copyright (c) 2013-2019 Christian Hesse <mail@eworm.de>
#
# reset bridge ports to default bridge

:global BridgePortTo;

:local Len ([ :len $BridgePortTo ] + 1);

:if ($Len = 1) do={
  :delay 1s;
  :set Len ([ :len $BridgePortTo ] + 1);
}

:foreach Interface in=[ / interface bridge port find where comment!="" ] do={
  :foreach Comment in=[ :toarray [ / interface bridge port get $Interface comment ] ] do={
    :if ([ :pick $Comment 0 $Len ] = ($BridgePortTo . ":")) do={
      :local InterfaceName [ / interface bridge port get $Interface interface ];
      :local BridgeDefault [ :pick $Comment $Len [ :len $Comment ] ];
      :local BridgeCurrent [ / interface bridge port get $Interface bridge ];
      :if ($BridgeDefault != $BridgeCurrent) do={
        :log info ("Changing interface " . $InterfaceName . " to " . $BridgePortTo . " bridge " . $BridgeDefault);
        / interface bridge port set bridge=$BridgeDefault $Interface;
        / ip dhcp-client renew [ find where interface=$BridgeDefault ];
      } else={
        :log debug ("Interface " . $InterfaceName . " already connected to " . $BridgePortTo . " bridge " . $BridgeDefault);
      }
    }
  }
}
