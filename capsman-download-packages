#!rsc
# RouterOS script: capsman-download-packages
# Copyright (c) 2018 Christian Hesse <mail@eworm.de>
#                    Michael Gisbers <michael@gisbers.de>
#
# requires: dont-require-permissions=yes
#
# download and cleanup packages for CAP installation from CAPsMAN

:global CertificateAvailable;

:local "package-path" [ / caps-man manager get package-path ];
:local "installed-version" [ / system package update get installed-version ];
:local updated false;

:foreach package in=[ / file find where type=package ] do={
  :if ([ / file get $package package-version ] != $"installed-version") do={
    :local "package-name" [ / file get $package package-name ];
    :local "package-architecture" [ / file get $package package-architecture ];
    :if ($"package-architecture" = "mips") do={
      :set "package-architecture" "mipsbe";
    }
    :if ($"package-name" = "wireless@") do={
      :set "package-name" "wireless";
    }
    :local "package-file" ($"package-name" . "-" . $"installed-version" . "-" . $"package-architecture" . ".npk");
    $CertificateAvailable "Let's Encrypt Authority X3" "letsencrypt";
    / tool fetch mode=https check-certificate=yes-without-crl \
      ("https://upgrade.mikrotik.com/routeros/" . $"installed-version" . "/" . $"package-file") \
      dst-path=($"package-path" . "/" . $"package-file");
    :set updated true;
    / file remove $package;
  }
}

:if ($updated = true) do={
  / caps-man remote-cap remove [ find where version!=$"installed-version" ];
}
