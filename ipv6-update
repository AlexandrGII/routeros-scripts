#!rsc
# RouterOS script: ipv6-update
# Copyright (c) 2013-2018 Christian Hesse <mail@eworm.de>
#
# update firewall and dns settings on IPv6 prefix change

:local pool [ / ipv6 pool get [ find where prefix=$"pd-prefix" ] name ];
:local addrlist [ / ipv6 firewall address-list find where comment=("ipv6-pool-" . $pool) ];
:local oldprefix [ / ipv6 firewall address-list get $addrlist address ];

# give the interfaces a moment to receive their addresses
:delay 2s;

if ($oldprefix != $"pd-prefix") do={
  :log info ("Updating IPv6 address list with new IPv6 prefix " . $"pd-prefix");
  / ipv6 firewall address-list set address=$"pd-prefix" $addrlist;

  :foreach record in=[ / ip dns static find where comment~("ipv6-pool-" . $pool) ] do={
    :local comment [ :toarray [ / ip dns static get $record comment ] ];
    :local intname [ :pick [ :pick $comment 1 ] 10 99 ];
    :local suffix [ :pick [ :pick $comment 2 ] 7 99 ];

    :local prefix [ / ipv6 address get [ find where interface=$intname from-pool=$pool global ] address ];
    :local prefix64 [ :pick $prefix 0 [ :find $prefix "::/64" ] ];

    :local name [ / ip dns static get $record name ];
    :if ([ :len $name ] = 0) do={
      :set name [ / ip dns static get $record regex ];
    }

    :log info ("Updating DNS record for " . $name . " to " . $prefix64 . ":" . $suffix);
    / ip dns static set address=($prefix64 . ":" . $suffix) $record;
  }
}
