# RouterOS script: email-daily-psk
# Copyright (c) 2013-2018 Christian Hesse <mail@eworm.de>
#
# email daily PSK (pre shared key)

:global "identity";
:global "email-general-to";
:global "email-general-cc";
:global "daily-psk-match-comment";

:local seen { "" };

:foreach acclist in=[ / interface wireless access-list find where comment~$"daily-psk-match-comment" ] do={
  :local skip 0;
  :local intname [ / interface wireless access-list get $acclist interface ];
  :local psk [ / interface wireless access-list get $acclist private-pre-shared-key ];
  :local interface [ / interface wireless find where name=$intname disabled=no ];
  :local ssid;

  :if ([ :len $interface ] = 1) do={
    :set ssid [ / interface wireless get $interface ssid ];
  } else={
    :log debug "Missing active interface for access list entry, skipping.";
    :set skip 1;
  }

  :foreach "seen-ssid" in=$seen do={
    :if ($"seen-ssid" = $ssid) do={
      :log debug ("Already sent a mail for SSID " . $ssid . ", skipping.");
      :set skip 1;
    }
  }

  :if ($skip = 0) do={
    :set $seen { $seen; $ssid };

    :local host "www.eworm.de"
    :local srcpath ("/cgi-bin/cqrlogo-wifi.cgi" . \
      "?scale=8" . \
      "&level=1" . \
      "&ssid=" . $ssid . \
      "&pass=" . $psk);

    / tool fetch mode=https check-certificate=yes-without-crl address=$host host=$host \
      src-path=$srcpath dst-path=qrcode-daily.png;

    / tool e-mail send to=$"email-general-to" cc=$"email-general-cc" \
      subject=("[" . $identity . "] daily PSK " . $ssid) \
      body=("This is the daily PSK on " . $identity . ":\n\n" . \
        "SSID: " . $ssid . "\n" . \
        "PSK:  " . $psk . "\n" . \
        "Date: " . [ / system clock get date ] . "\n\n" . \
        "https://" . $host . $srcpath) \
      file=qrcode-daily.png;
  }
}
