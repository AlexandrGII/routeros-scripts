#!rsc
# RouterOS script: check-certificates
# Copyright (c) 2013-2019 Christian Hesse <mail@eworm.de>
#
# check for certificate validity

:global Identity;
:global CertRenewUrl;
:global CertRenewPass;

:global SendNotification;

:local Months { "jan"; "feb"; "mar"; "apr"; "may"; "jun";
                "jul"; "aug"; "sep"; "oct"; "nov"; "dec" };

:local CurrentDate [ / system clock get date ];

:local CurrentDay [ :pick $CurrentDate 4 6 ];
:local CurrentYear [ :pick $CurrentDate 7 11 ];
:local CurrentMonth ([ :find $Months [ :pick $CurrentDate 0 3 ] ] + 1);
:local CurrentStamp ($CurrentYear * 365 + $CurrentMonth * 30 + $CurrentDay);

:foreach Cert in=[ / certificate find where !revoked ] do={
  :local CertName [ / certificate get $Cert name ];
  :local InvalidDate [ / certificate get $Cert invalid-after ];

  :if ([ :len $InvalidDate ] > 0) do={
    :local InvalidDay [ :pick $InvalidDate 4 6 ];
    :local InvalidYear [ :pick $InvalidDate 7 11 ];
    :local InvalidMonth ([ :find $Months [ :pick $InvalidDate 0 3 ] ] + 1);
    :local InvalidStamp ($InvalidYear * 365 + $InvalidMonth * 30 + $InvalidDay);

    :local Remaining ($InvalidStamp - $CurrentStamp);

    :if ($Remaining < 15) do={
      :local CommonName [ / certificate get $Cert common-name ];
      :local FingerPrint [ / certificate get $Cert fingerprint ];

      :do {
        :if ([ :len $CertRenewUrl ] = 0) do={
          :error "No CertRenewUrl given.";
        }

        / tool fetch mode=https check-certificate=yes-without-crl url=($CertRenewUrl . $CommonName . ".pem");
        / certificate import file-name=($CommonName . ".pem") passphrase=$CertRenewPass;
        / file remove [ find where name=($CommonName . ".pem") ];

        :local CertNew [ / certificate find where common-name=$CommonName fingerprint!=$FingerPrint ];
        :local CertNameNew [ / certificate get $CertNew name ];

        :foreach IpService in=[ / ip service find where certificate=$CertName ] do={
          / ip service set $IpService certificate=$CertNameNew;
        }

        :do {
          :foreach Hotspot in=[ / ip hotspot profile find where ssl-certificate=$CertName ] do={
            / ip hotspot profile set $Hotspot ssl-certificate=$CertNameNew;
          }
        } on-error={
          :log debug ("Setting hotspot certificates failed. Hotspot package not installed?");
        }

        / certificate remove $Cert;
        / certificate set $CertNew name=$CertName;
      } on-error={
        :log warning ("Failed to auto-update certificate " . $CertName);

        :local InvalidBefore [ / certificate get $Cert invalid-before ];
        :local InvalidAfter [ / certificate get $Cert invalid-after ];

        $SendNotification ("Certificate warning!") \
          ("A certificate on " . $Identity . " is about to expire.\n\n" . \
            "Certificate Name: " . $CertName . "\n" . \
            "Common Name:      " . $CommonName . "\n" . \
            "Fingerprint:      " . $FingerPrint . "\n" . \
            "Validity:         " . $InvalidBefore . " to " . $InvalidAfter);
        :log warning ("A certificate is about to expire within " . $Remaining . " days: " . $CertName);
      }
    } else={
      :log debug ("The certificate " . $CertName . " expires in " . $Remaining . " days.");
    }
  } else={
    :log debug ("The certificate " . $CertName . " is just a template.");
  }
}
